<div>
<p>
So far in my DevOps journey I've explored <a href="https://jarombek.com/blog/
sep-3-2018-terraform">Terraform</a> and how to use it to deploy <a href="https://jarombek.com/
blog/sep-7-2018-aws-lambda-api-gateway">AWS Lambda Infrastructure</a>.  In this post I explore
Jenkins, which is a Continuous Integration and Continuous Delivery (CI/CD) tool used for automating
tasks.
</p>
<SectionTitle title="Continuous Integration and Continuous Delivery">Continuous Integration and Continuous Delivery</SectionTitle>
<p>
Jenkins is not my first glance into the world on Continuous Integration (CI).  That came with my
post on <a href="https://jarombek.com/blog/mar-9-2018-travisci">TravisCI</a>, which is a CI tool
that hosts tasks on a server (free of charge!).  I still use TravisCI to help unit test my code
every time I push to GitHub.
</p>
<p>
Jenkins differs from TravisCI in a few ways.  To work with Jenkins you must set up a server for it
to run on.  Because of this, getting started with Jenkins is a longer process, and the learning
curve is greater.  However, once you get over the initial learning curve of Jenkins, you'll find a
richer ecosystem that allows for extensive customization.  This is due to the emergence of user
defined plugins which allow Jenkins tasks to be manipulated in ways outside of the core Jenkins
vision.
</p>
<p>
While TravisCI uses YAML for task definitions, Jenkins uses a UI and Groovy scripts.
Having the expressiveness of a full programming language for Jenkins tasks makes development fun!
</p>
<p>
Despite their differences, Jenkins and TravisCI are both CI/CD tools that are commonly used to
automate application deployments.
</p>
<Definition word="Continuous Integration">
The practice of merging code in a development environment into a main repository on a regular basis.
Code is merged every time a significant change is made.  When a code merge occurs, tests run to make
sure no parts of the application are broken.  Continuous Integration (CI) helps developers detect
bugs early, reducing development time and safeguarding application deployments.
</Definition>
<Definition word="Continuous Delivery">
The goal of short development cycles and configuring applications to be built, tested, and released at any
time<sup>1</sup>.  Continuous Delivery (CD) is possible through a deployment pipeline.  There are
many CD tools which allow for easy creation of pipelines, such as Jenkins and TravisCI.
</Definition>
<SectionTitle title="Jenkins Jobs and the DSL Plugin">Jenkins Jobs and the DSL Plugin</SectionTitle>
<p>
Automated tasks are called "jobs" in Jenkins.  There are a number of different job types in Jenkins,
the most popular being the Pipeline Job.  This job type creates CD pipelines using
a Groovy script<sup>2</sup>.  Jenkins created its own Groovy DSL for specifying pipeline steps,
although the full scope of the Groovy language can be used.
</p>
<p>
Typically, Jenkins jobs are created in the web GUI for the Jenkins server.  However, as I previously
mentioned, Jenkins has an extensive library of plugins that manipulate jobs and change the way we work
with Jenkins.  One of these plugins, the Jenkins Job DSL Plugin, specifies a DSL for creating Jenkins
jobs!
</p>
<p>
If you read my discovery post on <a href="https://jarombek.com/blog/sep-3-2018-terraform">Terraform
</a>, you'll remember my dislike for GUI configuration.  I'd much rather write code to configure my
infrastructure/CD pipelines.  Writing code is fun and allows the configuration to be checked
into version control.  With code, replicating infrastructure or a Jenkins job is as simple as
executing a script!
</p>
<p>
The rest of this discovery post will follow the basic setup for a "Hello World" Jenkins job using
the DSL Plugin.
</p>
<SectionTitle title="Jenkins Hello World Example">Jenkins "Hello World" Example</SectionTitle>
<p>
There are a number of different ways to install Jenkins (such as using a Docker container), however
I usually use the <a href="https://jenkins.io/download/">Generic Java Package</a> approach.  With
a Jenkins .war file on hand, starting a Jenkins server is as simple as executing
<code className="jarombek-inline-code">java -jar jenkins.war</code> in the terminal.
</p>
<p>
The Jenkins Job DSL Plugin does not install by default, so it must be added under Manage Jenkins >
Manage Plugins.  Once the DSL Plugin is added, seed jobs can be created that consume
Jenkins Job DSL Scripts<sup>3</sup>.  A seed job is the only job that needs to be created using
the traditional Jenkins web interface.
</p>
<p>
The DSL Plugin seed job is a Freestyle Jenkins job, which is the legacy job type used before
Pipeline Jobs were introduced in Jenkins 2<sup>4</sup>.  Freestyle Jobs don't use Groovy code to
specify their execution, instead relying on a web interface configuration.  However, with the Job DSL
Plugin, we can drop Groovy code into a text area in the web config.  The steps to create a seed job
are specified in detail on <a href="https://github.com/jenkinsci/job-dsl-plugin/wiki/Tutorial-
--Using-the-Jenkins-Job-DSL">GitHub</a>.  Here is the Groovy code used in the seed job:
</p>
<CodeSnippet language="Groovy">
// 'job' closure creates a new freestyle job.
// This type of Job is usually configured in the Jenkins UI,
// however with the DSL Plugin all configuration is in a Groovy script.
job("Seed_Job") {

  // Parameters for users of the Jenkins job to pass in
  parameters {
    // String parameters are simply single line text inputs.  The arguments of stringParam() are as follows:
    // (1) -> the name of the parameter that can be used later in the script as a variable.
    // (2) -> the default value of the parameter.  Defaults to null.
    // (3) -> the description of the parameter which will be displayed in Jenkins.
    stringParam("job_dsl_repo", "", "Job DSL Repo")
    stringParam("job_dsl_branch", "", "Job DSL Branch")
    stringParam("job_dsl_path", "", "Location of Job DSL Groovy Script")
  }

  // SCM (Source Control Management) allows the Jenkins pipeline to use different version control systems
  scm {
    // In my case, I use Git
    git {
      // From the repository specified in one of the Jenkins job parameters, checkout from a certain branch
      branch("\$job_dsl_branch")
      remote {
        name("origin")
        url("\$job_dsl_repo")
      }
    }
  }

  // Add build steps to the freestyle job.
  steps {

    // Add a Job DSL Plugin step to the freestyle job.  This step runs a Groovy script to build Jenkins jobs.
    dsl {
      // Read the Groovy script to build Jenkins job from the Jenkins workspace
      external("\$job_dsl_path")
    }
  }
}
</CodeSnippet>
<p>
The Jenkins Job DSL Plugin has some incredible <a href="https://jenkinsci.github.io/job-dsl-plugin/#">
documentation</a> on the Groovy DSL for creating jobs, but I did my best to inline document what was
happening above.  This script creates a second freestyle job called <code className="jarombek-inline-code">
Seed_Job</code>, which takes in three parameters.  <code className="jarombek-inline-code">Seed_Job</code> creates
new jobs by executing a Job DSL script in a Git repository.
</p>
<p>
After clicking “Build Now” in the seed job, I can see two jobs listed on the Jenkins server:
</p>
<figure>
<img className="jarombek-blog-image" src="https://asset.jarombek.com/posts/9-21-18-jenkins01.png"/>
</figure>
<p>
With Seed_Job all set, I created a new parameterized build with the location of one of my GitHub
repositories and the path to a Job DSL script.
</p>
<figure>
<img className="jarombek-blog-image" src="https://asset.jarombek.com/posts/9-21-18-jenkins02.png"/>
</figure>
<p>
Based of the parameters, Seed_Job will checkout the devops-prototypes repository from the master
branch and find the file jenkins/basic-dsl-plugin/basicJob.groovy.  This is what that file looks like:
</p>
<CodeSnippet language="Groovy">
// 'pipelineJob' creates a new pipeline Job
// which uses a Groovy script to execute
pipelineJob("Hello_World") {

  // Define the pipeline script which is located in Git
  definition {
    cpsScm {
      scm {
        git {
          branch("master")
          remote {
            name("origin")
            url("https://github.com/AJarombek/devops-prototypes.git")
          }
        }
      }
      // The path within source control to the pipeline jobs Jenkins file
      scriptPath("jenkins/basic-dsl-plugin/Jenkinsfile.groovy")
    }
  }
}
</CodeSnippet>
<p>
This Groovy script creates a Pipeline Job called <code className="jarombek-inline-code">Hello_World
</code>.  The script finds a Jenkinsfile in the devops-prototypes repository on the master branch.
This Jenkinsfile is what will execute every time the Hello_World job runs.
</p>
<p>
After clicking "Build", I looked at the console output from Seed_Job and saw that the new Hello_World pipeline job was created.
</p>
<figure>
<img className="jarombek-blog-image" src="https://asset.jarombek.com/posts/9-21-18-jenkins03.png"/>
</figure>
<p>
Now I see three jobs listed on the Jenkins server:
</p>
<figure>
<img className="jarombek-blog-image" src="https://asset.jarombek.com/posts/9-21-18-jenkins04.png"/>
</figure>
<p>
Running the Hello_World job executes the following Jenkinsfile:
</p>
<CodeSnippet language="Groovy">
node("master") {
  stage("Execute Bash Script") {

    // Log out the file structure of the jenkins job workspace
    sh """
      pwd
      ls -la
      ls -la ../
      ls -la ../Hello_World/
      ls -la ../Hello_World@script/
      ls -la ../Hello_World@tmp/
    """

    // Give Jenkins access to a bash script and then execute it
    sh """
      chmod +x ../Hello_World@script/jenkins/basic-dsl-plugin/script.sh
      ../Hello_World@script/jenkins/basic-dsl-plugin/script.sh
    """
  }
}
</CodeSnippet>
<p>
Jenkins pipeline scripts are written in one of two styles - scripted or declarative.  The Jenkinsfile
script for Hello_World follows the scripted syntax, which is written in the imperative paradigm<sup>5</sup>.
I won't give a full comparison between the two styles, but I picked the scripted style due to its
enhanced customization and flexibility<sup>6</sup>.  However, for simple pipelines like this either
method will suffice.
</p>
<p>
The pipeline code starts by specifying a node to run the job on with
<code className="jarombek-inline-code">node("master")</code>.  A node is a system that can run a Jenkins
job, and the master node is the default system for the Jenkins server<sup>7</sup>.  Usually jobs are
supposed to run on nodes other than master, but for a simple prototype its okay.
</p>
<p>
A stage specifies a logical grouping of steps in a pipeline<sup>8</sup>.  I specified one stage for
my pipeline - <code className="jarombek-inline-code">stage("Execute Bash Script")</code>.  Each
stage can have multiple steps, with each step being a command that executes.  My stage has two steps,
both of which execute commands in Bash.  The first step looks at all the files accessible to the
Jenkins pipeline, and the second step executes a simple script.sh file:
</p>
<CodeSnippet language="Bash">
#!/usr/bin/env bash

echo "Hello from Jenkins Pipeline"
date
</CodeSnippet>
<p>
Executing this pipeline produces the following output:
</p>
<figure>
<img className="jarombek-blog-image" src="https://asset.jarombek.com/posts/9-21-18-jenkins05.png"/>
</figure>
<p>
In this demo I used Seed_Job to generated a simple “Hello World” example.  However, it can also create
jobs of greater complexity.
</p>
<SectionTitle title="Conclusions">Conclusions</SectionTitle>
<p>
The jobs I created don't show the power of using Jenkins for Continuous Integration and Continuous
Delivery.  However, these seed jobs are building blocks to create more complex jobs.  I will save my
adventure into complex Jenkins pipelines for another discovery post.  All the code for this post
is on <a href="https://github.com/AJarombek/devops-prototypes/tree/master/jenkins/basic-dsl-plugin">
GitHub</a>.
</p>
</div>