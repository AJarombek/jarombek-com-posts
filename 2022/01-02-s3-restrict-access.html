<div>
<p>
Unsecured AWS S3 buckets have been a major source of data leaks in the Software Engineering industry for years.  Enter
a Google search for <a href="https://bit.ly/3ECsVSc">"S3 bucket public data leak"</a> and you will find countless
examples of hackers leaking data from Amazon S3.
</p>
<p>
The biggest reason why S3 data leaks are so common is that objects (files) in S3 buckets are easily misconfigured to be
public over HTTP.  When an S3 bucket is public, its contents are available for anyone in the world to view.  For
engineers new to AWS and the S3 service, the mistake of configuring S3 buckets to be public is very common.
</p>
<p>
Over the years, AWS has provided more ways to secure S3 buckets and provide explicit warnings to users if S3 buckets
are not properly protected.  AWS accounts can even have account-wide restrictions which prevent the creation of public
buckets.
</p>
<p>
In my AWS account, most of the S3 buckets have restrictions for who can access their contents.  However, just a week
ago, some buckets in my account were public, such as those used for static websites or those containing assets such as
images and fonts for my websites.  While there isn’t much danger in keeping these buckets public - they don’t contain
any sensitive data - it is still best practice to keep them private.  This article discusses how to restrict access to
these S3 buckets, while still keeping their contents publicly available via HTTP.  All the infrastructure changes
needed to restrict S3 access are written using Terraform.
</p>
<SectionTitle title="Problem to Solve">Problem to Solve</SectionTitle>
<p>
In the AWS Console, if an S3 bucket is publicly accessible, a warning icon is displayed.
</p>
<InlineImage filename="1-2-22-public-buckets.png" paddingTop="true" paddingBottom="true">
</InlineImage>
<p>
This warning is meant to deter users from leaving S3 buckets without any security.  In 2018, AWS added a "Block Public
Access" feature to S3, allowing engineers to restrict the creation of public buckets account-wide.  If an account’s
"Block Public Access" settings aren’t restrictive towards public S3 buckets, the AWS Console provides additional
warning messages.
</p>
<InlineImage filename="1-2-22-block-public-access.png" paddingTop="true" paddingBottom="true">
</InlineImage>
<p>
The public S3 buckets shown from my account are used as static websites.  These static websites are distributed and
given domain names using the AWS CloudFront CDN.  For example, one of my S3 buckets,
<strong>react16-3.demo.jarombek.com</strong>, is distributed using CloudFront and accessible at the
<a href="https://www.react16-3.demo.jarombek.com/">https://www.react16-3.demo.jarombek.com</a> domain.  Back in February
2020, I wrote an <a href="https://jarombek.com/blog/feb-15-2020-s3-react">article</a> about how to write the AWS
infrastructure for this static website using Terraform.
</p>
<p>
S3 buckets are also given Amazon domain names.  For example, my bucket has the domain
<a href="http://react16-3.demo.jarombek.com.s3-website-us-east-1.amazonaws.com/">
http://react16-3.demo.jarombek.com.s3-website-us-east-1.amazonaws.com/</a>.  For buckets with public access, anyone
with internet access can read the contents of the S3 bucket from this URL.
</p>
<p>
If you navigate to my S3 bucket’s Amazon domain name now, you will receive an HTTP 403 Forbidden error message.  This is
because my S3 bucket is now private.  However, at the time I took the AWS Console screenshots above (back when the
<strong>react16-3.demo.jarombek.com</strong> bucket was still public), navigating to
<a href="http://react16-3.demo.jarombek.com.s3-website-us-east-1.amazonaws.com/">
http://react16-3.demo.jarombek.com.s3-website-us-east-1.amazonaws.com/</a> showed the same static website as the
official CloudFront URL, <a href="https://www.react16-3.demo.jarombek.com/">https://www.react16-3.demo.jarombek.com</a>.
</p>
<InlineImage filename="1-2-22-static-website.png" paddingTop="true" paddingBottom="true">
</InlineImage>
<p>
While re-evaluating my S3 infrastructure recently, I decided it would be a good idea to make all my S3 buckets, even
those for static websites, private.  This way, their contents would be inaccessible through Amazon URLs, but still
accessible via the CloudFront CDN.  I also decided to change the "Block Public Access" settings for my account.  This
way, no S3 buckets can be made public in my account in the future.
</p>
<SectionTitle title="Restricting Access to S3 Buckets">Restricting Access to S3 Buckets</SectionTitle>
<p>
To demonstrate how I made my S3 buckets private, I’ll again use the <strong>react16-3.demo.jarombek.com</strong> bucket
as an example.  The Terraform infrastructure for this bucket exists on GitHub in my <a href="https://github.com/
AJarombek/jarombek-com-infrastructure/tree/v1.1.0/jarombek-com-react16-3-demo">jarombek-com-infrastructure</a>
repository.
</p>
<p>
The first piece of infrastructure to create is the <a href="https://github.com/AJarombek/jarombek-com-infrastructure/
blob/v1.1.0/jarombek-com-react16-3-demo/main.tf#L47-L61">S3 bucket</a>, which is provisioned with the
<code className="jarombek-inline-code">aws_s3_bucket</code> Terraform resource.
</p>
<CodeSnippet language="HCL">
resource "aws_s3_bucket" "react16-3-demo-jarombek" {
  bucket = "react16-3.demo.jarombek.com"
  acl = "private"

  tags = {
    Name = "react16-3.demo.jarombek.com"
    Environment = "production"
    Application = "react-16-3-demo"
  }

  website {
    index_document = "index.html"
    error_document = "index.html"
  }
}
</CodeSnippet>
<p>
In regards to security, the important argument in the <code className="jarombek-inline-code">aws_s3_bucket</code>
resource is <code className="jarombek-inline-code">acl</code>.  <code className="jarombek-inline-code">acl</code>
configures the Access Control List (ACL) value for the S3 bucket.  <code className="jarombek-inline-code">acl</code>
defaults to <code className="jarombek-inline-code">private</code>, which gives the owner of the S3 bucket (my AWS
account) full access to the bucket and provides no access for everyone else.  Back when my S3 buckets were publicly
accessible, the value of <code className="jarombek-inline-code">acl</code> was <code className="jarombek-inline-code">
public</code>.  The full list of ACL options for S3 buckets are available in the <a href="https://docs.aws.amazon.com/
AmazonS3/latest/userguide/acl-overview.html#canned-acl">AWS documentation</a>.
</p>
<p>
The next piece of infrastructure is a <a href="https://github.com/AJarombek/jarombek-com-infrastructure/blob/v1.1.0/
jarombek-com-react16-3-demo/main.tf#L85-L92">Public Access Block configuration for the S3 bucket</a>, which is
provisioned with the <code className="jarombek-inline-code">aws_s3_bucket_public_access_block</code> resource.
</p>
<CodeSnippet language="HCL">
resource "aws_s3_bucket_public_access_block" "react16-3-demo-jarombek" {
  bucket = aws_s3_bucket.react16-3-demo-jarombek.id

  block_public_acls = true
  block_public_policy = true
  restrict_public_buckets = true
  ignore_public_acls = true
}
</CodeSnippet>
<p>
Public Access Block configurations can be applied account-wide or to a specific S3 bucket.  The example above is
configured for an individual bucket.  Public Access Block configurations contain four settings, represented by the
<code className="jarombek-inline-code">block_public_acls</code>, <code className="jarombek-inline-code">
block_public_policy</code>, <code className="jarombek-inline-code">restrict_public_buckets</code>, and
<code className="jarombek-inline-code">ignore_public_acls</code> fields.  Giving these fields a value of
<code className="jarombek-inline-code">true</code> turns them on, while values of
<code className="jarombek-inline-code">false</code> (the default value) keep them off.  Since I want these settings
applied for additional security, I assign them values of <code className="jarombek-inline-code">true</code>.
</p>
<p>
For full details on what these four Public Access Block settings achieve, check out the
<a href="https://docs.aws.amazon.com/AmazonS3/latest/userguide/access-control-block-public-access.html#access-control-block-public-access-options">
AWS documentation</a> on the subject.  The gist is that with these settings applied, requests to create buckets or
objects in buckets with public IAM policies or public ACLs is prohibited.
</p>
<p>
As I mentioned earlier, Public Access Block settings can be applied to individual S3 buckets or an entire AWS account.
If Public Access Block settings are applied to both, the more restrictive of the two settings is enforced.  For example,
if the account-wide settings are turned on and the settings for a S3 bucket, let’s say
<strong>react16-3.demo.jarombek.com</strong>, are turned off, the restrictiveness of the account-wide settings are
applied to the bucket<sup>1</sup>.
</p>
<p>
Account-wide Public Access Block settings are created using a <code className="jarombek-inline-code">
aws_s3_account_public_access_block</code> resource.  The account-wide settings for my AWS account, which exist in my
<a href="https://github.com/AJarombek/global-aws-infrastructure/blob/v2.0.3/s3/main.tf#L52-L57">
global-aws-infrastructure</a> repository, are shown below.
</p>
<CodeSnippet language="HCL">
resource "aws_s3_account_public_access_block" "access" {
  block_public_acls = true
  block_public_policy = true
  restrict_public_buckets = true
  ignore_public_acls = true
}
</CodeSnippet>
<p>
With account-wide Public Access Block settings turned on, the AWS Console displays the following screen:
</p>
<InlineImage filename="1-2-22-s3-block-public-access-on.png" paddingTop="true" paddingBottom="true">
</InlineImage>
<p>
After the Public Access Block settings are properly configured, I create a CloudFront distribution for the S3 bucket
with the <code className="jarombek-inline-code">aws_cloudfront_distribution</code> resource.  The CloudFront
distribution also has a corresponding <code className="jarombek-inline-code">aws_cloudfront_origin_access_identity</code>
resource.  Shortened versions of both are shown below, with the full code available on <a href="https://github.com/
AJarombek/jarombek-com-infrastructure/blob/v1.1.0/jarombek-com-react16-3-demo/main.tf#L94-L174">GitHub</a>.
</p>
<CodeSnippet language="HCL">
resource "aws_cloudfront_distribution" "react16-3-demo-jarombek-distribution" {
  origin {
    domain_name = aws_s3_bucket.react16-3-demo-jarombek.bucket_regional_domain_name
    ...

    s3_origin_config {
      origin_access_identity = aws_cloudfront_origin_access_identity.origin-access-identity.cloudfront_access_identity_path
    }
  }

  ...
}

resource "aws_cloudfront_origin_access_identity" "origin-access-identity" {
  comment = "react16-3.demo.jarombek.com origin access identity"
}
</CodeSnippet>
<p>
As far as security is concerned, the <code className="jarombek-inline-code">aws_cloudfront_origin_access_identity</code>
resource is important for granting CloudFront access to a private S3 bucket.  It is assigned to the CloudFront
distribution with the <code className="jarombek-inline-code">origin.s3_origin_config.origin_access_identity</code>
Terraform attribute.  The CloudFront Origin Access Identity (OAI) is a user that can be assigned to CloudFront
distributions.  The key is to provide read access on a private S3 bucket to an OAI, thus giving CloudFront access to
the bucket's contents.
</p>
<p>
OAIs are given access to S3 buckets using <a href="https://github.com/AJarombek/jarombek-com-infrastructure/blob/
v1.1.0/jarombek-com-react16-3-demo/main.tf#L63-L83">S3 bucket policies</a>, created with the
<code className="jarombek-inline-code">aws_s3_bucket_policy</code> resource.  The following Terraform configuration
gives the OAI access to my S3 bucket.
</p>
<CodeSnippet language="HCL">
resource "aws_s3_bucket_policy" "react16-3-demo-jarombek" {
  bucket = aws_s3_bucket.react16-3-demo-jarombek.id
  policy = data.aws_iam_policy_document.react16-3-demo-jarombek.json
}

data "aws_iam_policy_document" "react16-3-demo-jarombek" {
  statement {
    sid = "CloudfrontOAI"

    principals {
      identifiers = [aws_cloudfront_origin_access_identity.origin-access-identity.iam_arn]
      type = "AWS"
    }

    actions = ["s3:GetObject", "s3:ListBucket"]
    resources = [
      aws_s3_bucket.react16-3-demo-jarombek.arn,
      "${aws_s3_bucket.react16-3-demo-jarombek.arn}/*"
    ]
  }
}
</CodeSnippet>
<p>
The <code className="jarombek-inline-code">aws_iam_policy_document</code> data source creates the S3 bucket policy
document.  It provides <code className="jarombek-inline-code">s3:GetObject</code> and
<code className="jarombek-inline-code">s3:ListObject</code> access to the OAI for the
<code className="jarombek-inline-code">react16-3.demo.jarombek.com</code> bucket.
</p>
<p>
After all this infrastructure is created, I have a private S3 bucket, whose contents are still accessible to the public
through a CloudFront distribution.  All the infrastructure shown in this article is available on
<a href="https://github.com/AJarombek/jarombek-com-infrastructure/blob/v1.1.0/jarombek-com-react16-3-demo">GitHub</a>.
</p>
<SectionTitle title="Conclusions">Conclusions</SectionTitle>
<p>
Keeping S3 buckets private, even when they hold publicly accessible assets such as static website files, is critical
for cloud infrastructure security.  The approach of using a CloudFront OAI to access a private S3 bucket is very easy
to implement, and AWS recommends implementing it.
</p>
</div>
