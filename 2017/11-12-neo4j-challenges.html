<div>
<p>
In my last discovery post on graph databases and Neo4j, I <a href="https://jarombek.com/blog/nov-6-2017-neo4j-create">
created a graph</a> representing a map of Fairfield County, Connecticut.  I created nodes
for all the towns/cities and edges between the settlements that shared borders.  This discovery adds
to the graph and shows some of the challenges I faced along the way.  Let's dive in!
</p>
<p>
First I populated the graph with some people (after all, a settlement needs citizens!).
</p>
<CodeSnippet language="Cypher">
CREATE (aj:Person {name: 'Andy J.'}),
    (tc:Person {name: 'Tom C.'}),
    (js:Person {name: 'Joe S.'}),
    (bf:Person {name: 'Ben F.'})
</CodeSnippet>
<p>
If I want to undo this <code className="jarombek-inline-code">CREATE</code> statement, one
option is to simply delete all the people from the graph:
</p>
<CodeSnippet language="Cypher">
MATCH (p:Person) DELETE p
</CodeSnippet>
<p>
However, maybe I just want specific people removed from the graph.  The first idea that came into to my head
was to match on a certain name such as <code className="jarombek-inline-code">MATCH (p:Person {name: 'Andy J.'})</code>.
In this case I would delete all nodes with label <code className="jarombek-inline-code">:Person</code> where the property <code className="jarombek-inline-code">name</code> equals 'Andy J.'.
You may see the potential issue here.  What if there are two people with the name 'Andy J'?  In that case
Cypher deletes both of the vertices!  In a large graph that scenario is very likely.
What I can do instead is delete a vertex by looking up its node id.  Each vertex and
edge has a unique id, so if I delete a vertex by its id I know only one vertex will be removed.  Node
ids are accessed with the Cypher <code className="jarombek-inline-code">id()</code> function<sup>1</sup>.
</p>
<CodeSnippet language="Cypher">
MATCH (p:Person) WHERE id(p) = 62 DETACH DELETE p
</CodeSnippet>
<p>
This example searches for a node where the id equals 62 and deletes it.  The pair of keywords
I haven't used before are <code className="jarombek-inline-code">DETACH DELETE</code>.  In order to delete
a node in Cypher you also have to delete its relationships, so <code className="jarombek-inline-code">
DETACH DELETE</code> removes both<sup>2</sup>.
</p>
<p>
If all four people vertices are back in the graph, I can create a relationship between them
and the settlements they are citizens of:
</p>
<CodeSnippet language="Cypher">
MATCH (greenwich:Town {name: 'Greenwich'}),
    (sherman:Town {name: 'Sherman'}),
    (fairfield:Town {name: 'Fairfield'}),
    (easton:Town {name: 'Easton'})
CREATE (aj:Person {name: 'Andy J.'}),
    (aj)-[:LIVES_IN]->(greenwich),
    (tc:Person {name: 'Tom C.'}),
    (tc)-[:LIVES_IN]->(fairfield),
    (js:Person {name: 'Joe S.'}),
    (js)-[:LIVES_IN]->(easton),
    (bf:Person {name: 'Ben F.'}),
    (bf)-[:LIVES_IN]->(sherman)
</CodeSnippet>
<p>
The settlements are now populated with people!  However, Fairfield County still seems a bit boring.  Let's populate it
with the top rated pizza places in the county.  I created pizza vertices and gave them a
<code className="jarombek-inline-code">:LOCATED_IN</code> relationship to a settlement:
</p>
<CodeSnippet language="Cypher">
CREATE (stan:Pizza {name: 'Stanziatos Wood Fired Pizza', price: 2, rating: 4.5}),
    (stan)-[:LOCATED_IN]->(:Settlement {name: 'Danbury'}),
    (bw:Pizza {name: 'Brick + Wood', price: 2, rating: 4.5}),
    (bw)-[:LOCATED_IN]->(:Settlement {name: 'Fairfield'}),
    ...
</CodeSnippet>
<p>
When I first ran this code I really thought it would work!  However, instead of connecting the newly
created pizza places to existing settlements, Cypher created new settlements as well!  Now there are
duplicate settlements in the graph that need to be deleted.  The following query does the job<sup>3</sup>:
</p>
<CodeSnippet language="Cypher">
MATCH (s:Settlement)
WITH s.name as name, collect(s) as instances
WHERE size(instances) > 1
UNWIND tail(instances) as dups
MATCH (dups)-[r:LOCATED_IN]-(pizza)
DELETE r, dups, pizza
</CodeSnippet>
<p>
Let's walk through this.  First I queried the graph for all settlements.  Then I
set up a table with two columns.  The first column contains the name of a
settlement and the second column contains a list of all settlement vertices with that name.
The first column is created with the statement <code className="jarombek-inline-code">WITH s.name as name</code>.
The <code className="jarombek-inline-code">WITH</code> clause is used to manipulate data (in this case
the settlement vertices) before its passed to the next line in the query<sup>4</sup>.
The <code className="jarombek-inline-code">AS</code> keyword creates an alias for a column.
I created a list of all the settlement vertices with a given name with Cypher's
<code className="jarombek-inline-code">collect()</code> function.  The entire second line
of code in the sample above is basically an implicit <code className="jarombek-inline-code">GROUP BY</code> statement (like those
in SQL).  Cypher has no explicit <code className="jarombek-inline-code">GROUP BY</code> syntax.
</p>
<p>
Next, the line <code className="jarombek-inline-code">WHERE size(instances) > 1</code> filters the
<code className="jarombek-inline-code">instances</code> list down to those with multiple vertices.  The following line, <code className="jarombek-inline-code">
UNWIND tail(instances) as dups</code> transforms the list back into table rows<sup>5</sup>. Finally we see if
a table row (which is a settlement) has a <code className="jarombek-inline-code">LOCATED_IN</code> relationship to a vertex with
label <code className="jarombek-inline-code">:Pizza</code>.  If so the settlement is duplicated data.  In the final
line, I deleted the settlement, relationship, and pizza place from the graph.  The graph is successfully
cleaned up!
</p>
<p>
Let's again create pizza places and relate them to a settlement without creating duplicates:
</p>
<CodeSnippet language="Cypher">
CREATE (p1:Pizza {name: 'Stanziatos Wood Fired Pizza', price: 2, rating: 4.5})
WITH p1
MATCH (s1:Settlement {name: 'Danbury'})
MERGE (p1)-[:LOCATED_IN]->(s1)
</CodeSnippet>
<p>
First I created a pizza place and passed it to the <code className="jarombek-inline-code">MATCH</code>
statement via the <code className="jarombek-inline-code">WITH</code> clause.  This sequence uses
<code className="jarombek-inline-code">WITH</code> for variable passing without performing any
manipulations.  Instead of creating a new settlement I used <code className="jarombek-inline-code">MATCH</code>
to find an existing one.  I then used <code className="jarombek-inline-code">MERGE</code> to create a <code className="jarombek-inline-code">
LOCATED_IN</code> relationship between the settlement and the pizza place. <code className="jarombek-inline-code">MERGE</code> only creates a new
relationship if one does not already exist.  If a relationship does not exist,
<code className="jarombek-inline-code">MERGE</code> acts the same as <code className="jarombek-inline-code">CREATE</code>.
</p>
<p>
Now the graph is complete with settlements, people, and places to eat.  One last important thing to mention
is indexes.  In Neo4j indexes are used to speed up vertex lookup speeds<sup>6</sup>.
This is important when searching for a specific item in a graph, such as a person with the name Tom.
</p>
<p>
Since settlements and pizza places are often queried by their names, I created indexes on those
labels and properties like so:
</p>
<CodeSnippet language="Cypher">
CREATE INDEX ON :Settlement(name)
CREATE INDEX ON :Pizza(name)
</CodeSnippet>
<p>
Now the graph database is ready to be queried.  The code used for this discovery can be found on <a href="https://
github.com/AJarombek/jarombek-com-sources/blob/master/2017/11-Nov/11-12-Neo4j-Challenges/neo4j-additions.cql">GitHub</a>.
</p>
</div>